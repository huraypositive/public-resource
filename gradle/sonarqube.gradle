jacoco {
    toolVersion = "0.8.8"
}

tasks.named('test') {
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["*Controller*", "*.Q*"])
        }))
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                minimum = 0.40
            }

            excludes = ["*Controller*", "*.Q*"]
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "${archivesBaseName}"
        property "sonar.projectVersion", "${version}"
        property "sonar.host.url", "${sonarqubeHostUrl}"
        property "sonar.login", "${sonarqubeLogin}"
        property "sonar.sources", "src"
        property "sonar.language", "java"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.exclusions", "**/*Test*.*, **/Q*.kt, **/Q*.java"
        property "sonar.test.inclusions", "**/*Test.groovy, **/*Test.kt, **/*Test.java"
        property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}
